plugins {
  id 'groovy'
  id 'idea'
  id 'java-library'
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'nu.studer.jooq' version '7.1.1'
  id 'org.flywaydb.flyway' version '8.5.8'
}

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor("org.projectlombok:lombok:1.18.22")
  annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"

  api 'io.vavr:vavr:0.10.4'
  api "com.google.code.findbugs:jsr305:3.0.2"

  api project(':infrastructure:failure')
  api project(':business:api')
  api project(':business:model')
  api project(':infrastructure:jooq')
  api project(':infrastructure:bean-validation:jakarta')

  api "org.slf4j:slf4j-api:1.7.36"

  compileOnly("org.projectlombok:lombok:1.18.22")

  implementation "org.jooq:jooq:3.16.4"
  implementation "org.jooq:jooq-codegen:3.16.4"
  implementation "org.jooq:jooq-meta:3.16.4"

  implementation "org.flywaydb:flyway-core:8.5.9"
  implementation "com.github.javafaker:javafaker:1.0.2"
  implementation "org.apache.commons:commons-lang3:3.12.0"
  implementation "org.mapstruct:mapstruct:1.4.2.Final"

  runtimeOnly "org.postgresql:postgresql:42.3.3"

  jooqGenerator "org.jooq:jooq-meta-extensions:3.16.4"
  jooqGenerator "org.postgresql:postgresql:42.3.3"

  testImplementation "org.codehaus.groovy:groovy:3.0.10"
  testImplementation "org.spockframework:spock-core:2.1-groovy-3.0"

  testImplementation "org.apache.commons:commons-dbcp2:2.9.0"

  testImplementation "org.testcontainers:spock:1.17.1"
  testImplementation "org.testcontainers:postgresql:1.16.3"
}

idea {
  module {
    sourceDirs += new File("${ project.projectDir }/build/generated-src/jooq/main")
  }
}

jooq {
  configurations {
    main {
      generationTool {
        logging = 'WARN'
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
            properties {
              property {
                key = 'scripts'
                value = "src/main/resources/db/migration/*.sql"
              }
              property {
                key = 'defaultNameCase'
                value = 'lower'
              }
              property {
                key = 'parseIgnoreComments'
                value = true
              }
            }
          }
          target {
            packageName = 'vavr.talk.javamexico.persistence.jooq'
          }
          strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
        }
      }
    }
  }
}

java {
  sourceCompatibility = JavaVersion.toVersion("17")
  targetCompatibility = JavaVersion.toVersion("17")
}

testing {
  suites {
    // Configure the built-in test suite
    test {
      // Use Spock test framework
      useSpock('2.0-groovy-3.0')
    }
  }
}

flyway {
  url = "jdbc:postgresql://localhost:5434/investing"
  user = 'admin'
  password = 'password'
  schemas = ['public']
  locations = ['classpath:db/migration'] //needed to be aware of Java based migrations
  skipDefaultCallbacks = true
}

processResources.dependsOn generateJooq
flywayInfo.dependsOn classes
flywayMigrate.dependsOn classes
